// CarGame

class Car {
  PImage img;
  float x, y;

  Car(float startX, float startY) {
    x = startX;
    y = startY;
    img = createCarImage();
  }

  void draw() {
    imageMode(CENTER);
    image(img, x, y);
  }

  void moveLeft() {
    x -= 20;
  }

  void moveRight() {
    x += 20;
  }

  PImage createCarImage() {
    PGraphics pg = createGraphics(40, 60);
    pg.beginDraw();
    pg.background(0, 0);
    pg.fill(0);
    pg.rect(7, 40, 5, 10);
    pg.rect(27, 40, 5, 10);
    pg.rect(7, 10, 5, 10);
    pg.rect(27, 10, 5, 10);
    pg.fill(255, 0, 0);
    pg.rect(10, 10, 20, 40);
    pg.endDraw();
    return pg.get();
  }
}

// main

class Game {
  Car car;
  ArrayList<Obstacle> obstacles;
  float roadWidth = 200;
  float roadX;
  float scrollY = 0;
  float speed = 4;
  int score = 0;
  boolean crashed = false;

  Game() {
    roadX = width / 2 - roadWidth / 2;
    car = new Car(width / 2, height - 100);
    obstacles = new ArrayList<Obstacle>();
    spawnObstacles();
  }

  void update() {
    if (!crashed) {
      scrollY += speed;
      drawRoad(scrollY);
      updateObstacles();
      checkCollisions();
      drawObstacles();
      car.draw();
      drawScore();
    } else {
      fill(255, 0, 0);
      textSize(32);
      textAlign(CENTER);
      text("CRASH!", width / 2, height / 2);
      text("Score: " + score, width / 2, height / 2 + 40);
    }
  }


  void drawRoad(float yOffset) {
    stroke(255);
    fill(100);
    for (int y = -100; y < height + 100; y += 40) {
      float yPos = y + yOffset % 40;
      rect(roadX, yPos, roadWidth, 40);
      line(roadX + roadWidth / 2, yPos, roadX + roadWidth / 2, yPos + 40);
    }
  }

  void drawScore() {
    fill(255);
    textSize(18);
    textAlign(LEFT);
    text("Score: " + score, 10, 20);
  }

  void spawnObstacles() {
    for (int i = 0; i < 10; i++) {
      float x = random(roadX + 20, roadX + roadWidth - 20);
      float y = -i * 150;
      obstacles.add(new Obstacle(x, y));
    }
  }

  void updateObstacles() {
    for (int i = obstacles.size() - 1; i >= 0; i--) {
      Obstacle obs = obstacles.get(i);
      obs.update(speed);

      if (obs.pos.y > car.y + 30) {
        score++;
      }

      if (obs.isOffScreen(height)) {
        obstacles.remove(i);
        float x = random(roadX + 20, roadX + roadWidth - 20);
        float y = -150;
        obstacles.add(new Obstacle(x, y));
      }
    }
  }

  void drawObstacles() {
    for (Obstacle obs : obstacles) {
      obs.draw();
    }
  }

  void checkCollisions() {
    for (Obstacle obs : obstacles) {
      if (obs.collidesWith(car)) {
        crashed = true;
      }
    }

    if (car.x < roadX + 20 || car.x > roadX + roadWidth - 20) {
      crashed = true;
    }
  }

  void reset() {
    car = new Car(width / 2, height - 100);
    scrollY = 0;
    score = 0;
    crashed = false;
    obstacles.clear();
    spawnObstacles();
  }
}

// MainSketch

Game game;

void setup() {
  size(400, 600);
  game = new Game();
}

void draw() {
  background(50, 150, 50);
  game.update();
}

void keyPressed() {
  if (!game.crashed) {
    if (keyCode == LEFT) game.car.moveLeft();
    if (keyCode == RIGHT) game.car.moveRight();
  } else {
    if (key == ENTER) {
      game.reset();
    }
  }
}

